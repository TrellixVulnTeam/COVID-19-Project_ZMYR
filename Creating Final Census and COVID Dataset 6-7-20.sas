
PROC DATASETS NOPRINT LIB=WORK KILL;
RUN;
QUIT;

DM LOG "CLEAR;";

*IMPORTING CENSUS DATA;
*//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////;

	%MACRO IMPORTER(DSET, FILE);
	PROC IMPORT OUT = T_&DSET DATAFILE= "C:\USERS\DANIEL CHACRETON\DOCUMENTS\PYTHON\USF\COVID-19 PROJECT\DATA\&FILE"
	DBMS = XLSX REPLACE;
	GETNAMES= YES;
	DATAROW = 2;
	RUN;

	DATA &DSET;
	LENGTH COUNTY $20.;
	SET T_&DSET;
	IF  FIND(GEO_ID, "ID", "I") THEN DELETE;

	COUNTY = UPCASE(SUBSTR(NAME, 1, INDEX(NAME," County")));
	RUN;


	PROC SORT DATA =&DSET;
	BY COUNTY;
	RUN;

	%MEND;

	%IMPORTER(INC1  ,CENSUS\FLORIDA 2018 MEDIAN INCOME.XLSX);
	%IMPORTER(EDU1  ,CENSUS\FLORIDA EDUCATION 2018.XLSX);
	%IMPORTER(RACE1 ,CENSUS\FLORIDA RACE & POP 2018.XLSX);
	%IMPORTER(AGE1 ,CENSUS\FLORIDA MEDIAN AGE 2018.XLSX);

	%MACRO SORTI(DSET, VAR1, VAR2, VAR3);
	PROC SORT DATA =&DSET;
	BY &VAR1 &VAR2 &VAR3;
	RUN;
	%MEND;

	DATA INC;
	SET INC1;
	KEEP S1901_C01_012E COUNTY;
	RUN;

	DATA RACE;
	SET RACE1;
	KEEP COUNTY DP05_0001E DP05_0037PE DP05_0038PE DP05_0039PE DP05_0044PE DP05_0052PE;
	RUN ;


	DATA AGE;
	SET AGE1;
	KEEP COUNTY S0101_C01_032E;
	RUN ;


	DATA EDU;
	SET EDU1;
	KEEP COUNTY S1501_C02_002E S1501_C02_009E ;
	RUN ;

	%SORTI(AGE, COUNTY);
	%SORTI(EDU , COUNTY);
	%SORTI(INC , COUNTY);
	%SORTI(RACE , COUNTY);

	DATA FINAL_CENSUS;
	MERGE AGE EDU INC RACE;
	BY COUNTY;
	RUN;
	PROC DATASETS NOPRINT; 
	DELETE AGE AGE1 EDU EDU1 INC INC1 RACE RACE1 t_INC1 T_EDU1 T_AGE1 T_RACE1;
	RUN;
	QUIT;


*IMPORTING COVID DATA;
*//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////;

	proc import out = covid1 datafile= "C:\Users\Daniel chacreton\Documents\Python\USF\COVID-19 Project\Data\Txt\Output\Complete COVID Data Set (unclean) (2020-06-07).xlsx"
	DBMS = XLSX REPLACE;
	GETNAMES = YES;
	DATAROW=2;
	RUN;

	DATA COVID;
	SET COVID1;
	COUNTY = UPCASE(COUNTY);
	ARRAY CONSOL AWAITING_TESTING AWAITING_BPHL_TESTING AWAITING_TESTING_1;
		DO OVER CONSOL;
		IF CONSOL NE . THEN AWAIT_TEST = CONSOL;
		END;
	ARRAY CONSOL2 TOTAL_TESTED TOTAL;
		DO OVER CONSOL2;
		IF CONSOL2 NE . THEN TOT = CONSOL2;
		END;
	IF AWAIT_TEST =. THEN AWAIT_TEST = 0;
	IF INCONCLUSIVE =. THEN INCONCLUSIVE = 0;
	IF COUNTY = "" THEN COUNTY = "UNKNOWN";
	IF COUNTY = "TOTAL" THEN DELETE;
	IF COUNTY = "DADE" THEN COUNTY = "MIAMI-DADE";
	DROP AWAITING_TESTING AWAITING_BPHL_TESTING AWAITING_TESTING_1 TOTAL_TESTED TOTAL;
	RENAME AWAIT_TEST = AWAITING_TESTING TOT = TOTAL_TESTED;
	RUN;

	%SORTI(COVID, COUNTY);
	%SORTI(FINAL_CENSUS, COUNTY);

	DM LOG "CLEAR;";

	DATA FINAL_COMPLETE;
	MERGE COVID FINAL_CENSUS;
	BY COUNTY;
	DATE1 = INPUT(COMPRESS(TRanWrD(DATE,"-","/")),MMDDYY10.);
	RANDOM = RANUNI(3542);
	FORMAT DATE1 MMDDYY10.;
	KEEP RANDOM COUNTY AWWAITING_TESTING DATE1 DATE2 FILE_NAME	INCONCLUSIVE	NEGATIVE	POSITIVE	PERCENT_POSITIVE	TOTAL_TESTED	
		S1901_C01_012E S1501_C02_002E S1501_C02_009E DP05_0001E DP05_0037PE DP05_0038PE DP05_0039PE
	DP05_0044PE DP05_0052PE NAME GEO_ID S0101_C01_032E NEW_NEGATIVE NEW_CASES FILENAME;
	RENAME DATE1 = DATE;
	RUN;
	QUIT;

	%MACRO IMPORTER2(DSET, FILEPATH, SHEET);
	PROC IMPORT OUT = &DSET DATAFILE= &FILEPATH
	DBMS=XLSX REPLACE;
	GETNAMES= YES;
	SHEET = &SHEET;
	RUN;
	%MEND;
	
	%IMPORTER2(DICT, "C:\Users\Daniel chacreton\Documents\Python\USF\COVID-19 Project\Data\Data Dictionary.xlsx", "DATA DICTIONARY");
	%IMPORTER2(VALID, "C:\Users\Daniel chacreton\Documents\Python\USF\COVID-19 Project\Data\Data Dictionary.xlsx", "Validation Check");

	%MACRO EXPORTER (DSET, FILEPATH, SHEET);
	PROC EXPORT DATA = &DSET OUTFILE = &FILEPATH
	DBMS = XLSX REPLACE;
	SHEET= &SHEET;
	RUN;
	%MEND;

		%EXPORTER (FINAL_COMPLETE, "C:\Users\Daniel chacreton\Documents\Python\USF\COVID-19 Project\Data\Final Combined Dataset COVID & Census (6-6-20)", "PROJECT_DATASET");
		%EXPORTER (DICT, "C:\Users\Daniel chacreton\Documents\Python\USF\COVID-19 Project\Data\Final Combined Dataset COVID & Census (6-6-20)", "DICTIONARY");
		%EXPORTER (VALID, "C:\Users\Daniel chacreton\Documents\Python\USF\COVID-19 Project\Data\Final Combined Dataset COVID & Census (6-6-20)", "vALIDATION");



PROC DATASETS NOPRINT; 
DELETE COVID1 COVID FINAL_CENSUS;
RUN;
QUIT;


DM LOG "CLEAR;";
DM ODSRESULTS 'CLEAR;';
%SORTI(FINAL_COMPLETE, DATE);
*DATA ANALYSIS;
*//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////;

*DATA VALIDATION;
	*//////////////////////////////////////;
	PROC FREQ DATA = FINAL_COMPLETE;
	TABLES NEW_CASES;
	RUN;

	PROC FREQ NOPRINT DATA = FINAL_COMPLETE;
	TABLES FILE_NAME/ OUT=FILE_FREQ;
	RUN;

		PROC PRINT DATA = FILE_FREQ;
		WHERE COUNT NE 68;
		TITLE'COVID Text Files With More or Fewer ';
		title2'Than the Expected 68 Observations';
		RUN;

*CHECKING ASSUMPTIONS;
	*//////////////////////////////////////;
	PROC UNIVARIATE DATA = FINAL_COMPLETE NORMAL PLOTS;
	VAR NEW_CASES;
	HISTOGRAM NEW_CASES /NORMAL ;
	RUN;

*DESCRIPTIVE ANALYSIS;
	*//////////////////////////////////////;
	PROC FREQ NOPRINT DATA =FINAL_COMPLETE;
	TABLES COUNTY / OUT = COUNTY_N;
	RUN;

		PROC MEANS N MIN MAX RANGE DATA = COUNTY_N;
		VAR COUNT;
		RUN;

	PROC MEANS noprint SUM DATA = FINAL_COMPLETE;
	BY DATE;
	VAR NEW_CASES;
	OUTPUT OUT= SUM_BY_DATE SUM=NEW_CASES;
	RUN;

	PROC MEANS DATA = FINAL_COMPLETE;
	VAR NEW_CASES;
	RUN;
*ADD A ODS OUTPUT FORMAT TO MAKE THE GRAPH BIGGER;
	PROC SGPLOT DATA =FINAL_COMPLETE;
	SERIES X = DATE Y = NEW_CASES/ GROUP = COUNTY;
	TITLE 'Spagetti Plot Daily Number of New Cases';
	RUN;

	PROC SGPLOT DATA = SUM_BY_DATE;
	SERIES X = DATE Y = NEW_CASES;
	TITLE'Number of New COVID-19 Cases State-wide';
	RUN;
*MODELING;
	*//////////////////////////////////////;

